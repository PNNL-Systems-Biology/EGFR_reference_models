// Function
function ras(t)
12.69798 - 2 * 1.9101 * t + 3 * 0.08355 * t ^ 2
end

model RAF_activation()

// Species

// 'b' means bound at both sites (bi)
// 'u' means bound at one sites (uni)

species RAS
species ppRAF_d1433b_MEK
species RASu_ppRAF_d1433b_MEK
species RASu_ppRAF_d1433u_MEK
species RASb_ppRAF_d1433u_MEK
species MRAS_SHOC2_PP1C
species MRAS_SHOC2_PP1C_RASb_ppRAF_d1433u_MEK
species RASb_pRAF_d1433u_MEK
species RAFa_MEK, RAFa_pMEK, RAFa_ppMEK, tRAFa_ppMEK
species ERK, pERK, ppERK

// Reactions

// Notes: RAS nanoclustering is not taken into account here
// Notes: RAS is assumed to be in the GTP bound state

J0: -> RAS; ras(time)

// Ras binds RBD domain
J1: RAS + ppRAF_d1433b_MEK -> RASu_ppRAF_d1433b_MEK; k1 * RAS * ppRAF_d1433b_MEK - k_1 * RASu_ppRAF_d1433b_MEK

// d1433 releasd from C-terminal phosphorylation site
J2: RASu_ppRAF_d1433b_MEK -> RASu_ppRAF_d1433u_MEK; k2 * RASu_ppRAF_d1433b_MEK - k_2 * RASu_ppRAF_d1433u_MEK

// RAS binds CRD domain
J3: RASu_ppRAF_d1433u_MEK -> RASb_ppRAF_d1433u_MEK; k3 * RASu_ppRAF_d1433u_MEK - k_3 * RASb_ppRAF_d1433u_MEK

// MRAS_SHOC2_PP1C complex dephosphorylates the C-terminal site
J4: MRAS_SHOC2_PP1C + RASb_ppRAF_d1433u_MEK -> MRAS_SHOC2_PP1C_RASb_ppRAF_d1433u_MEK; k4 * MRAS_SHOC2_PP1C * RASb_ppRAF_d1433u_MEK - k_4 * MRAS_SHOC2_PP1C_RASb_ppRAF_d1433u_MEK
J5: MRAS_SHOC2_PP1C_RASb_ppRAF_d1433u_MEK -> MRAS_SHOC2_PP1C + RASb_pRAF_d1433u_MEK; k5 * MRAS_SHOC2_PP1C_RASb_ppRAF_d1433u_MEK

// RAF complex dimerization (active RAF or RAFa_MEK)
// Notes:
// The reverse (unbinding of the dimer is not taken into account here)
J6: 2 RASb_pRAF_d1433u_MEK -> RAFa_MEK; k6 * RASb_pRAF_d1433u_MEK^2

// MEK phosphorylation
J7: RAFa_MEK -> RAFa_pMEK; k7 * RAFa_MEK - k_7 * RAFa_pMEK
J8: RAFa_pMEK -> RAFa_ppMEK; k8 * RAFa_pMEK - k_8 * RAFa_ppMEK

// ERK phosphorylation
J9: RAFa_ppMEK + ERK -> RAFa_ppMEK_ERK; k9 * RAFa_ppMEK * ERK - k_9 * RAFa_ppMEK_ERK
J10: RAFa_ppMEK_ERK -> RAFa_ppMEK + pERK; k10 * RAFa_ppMEK_ERK
J11: RAFa_ppMEK + pERK -> RAFa_ppMEK_pERK; k11 * RAFa_ppMEK * pERK - k_11 * RAFa_ppMEK_pERK
J12: RAFa_ppMEK_pERK -> RAFa_ppMEK + ppERK; k12 * RAFa_ppMEK_pERK
J13: pERK -> ERK; k13 * pERK
J14: ppERK -> pERK; k14 * ppERK

tRAFa_ppMEK := RAFa_ppMEK + RAFa_ppMEK_ERK + RAFa_ppMEK_pERK

// initial values
ppRAF_d1433b_MEK = 15.4
MRAS_SHOC2_PP1C = 15
ERK = 153

// Parameters
k1 = 6.440827339076919
k_1 = 0.255711662013132
k2 = 9.365589918551734
k_2 = 1.0557218748582575
k3 = 9.95816930322606
k_3 = 0.5777318057536327
k4 = 8.866823716327152
k_4 = 0.3898858169015143
k5 = 9.828730072765731
k6 = 8.718175498161447
k7 = 9.383669068558136
k_7 = 0.1548981883007705
k8 = 0.5472075321406964
k_8 = 9.982932439992434
k9 = 0.5139636890354343
k_9 = 0.453284422132897
k10 = 9.997577885390072
k11 = 9.96491541020991
k_11 = 0.1660871904270286
k12 = 9.997835998716305
k13 = 0.9515183995910566
k14 = 0.2958098336110293

end
